apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version photoselectorVersionName
group 'software.9to5'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "borekbeker925"
            name "Borek Beker"
            email "borek.beker@9to5.software"
        }
    }

    scm {
        url "https://github.com/Borek321/9to5-tools"
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact sourcesJar
            artifact "$buildDir/outputs/aar/photoselector-release.aar"

            groupId 'software.9to5'
            artifactId 'photoselector'
            version photoselectorVersionName
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Select a photo from camera or gallery')
                root.appendNode('name', 'Photo Selector')
                root.appendNode('url', 'https://github.com/Borek321/9to5-tools')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def bintrayUser = properties.getProperty('bintrayUser')
    def bintrayApiKey = properties.getProperty('bintrayApiKey')

    user = bintrayUser
    key = bintrayApiKey
    publications = ['Production']
    configurations = ['archives']
    override = true

    pkg {
        repo = '9to5-tools'
        name = 'Photoselector'
        description = "Select a photo from camera or gallery"
        publish = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        version {
            name = photoselectorVersionName
        }
    }
}

